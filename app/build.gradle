apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def keystorePropertiesFile = file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    signingConfigs {
        AdicTIC {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 31
    def code = getCustomVersionCode()
    defaultConfig {
        applicationId "com.adictic.client"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode code
        versionName "1.0."+code
        signingConfig signingConfigs.AdicTIC
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            // https://github.com/android/ndk-samples/blob/8132651aba8db36b14e0d0461c7cb46d3778f99c/other-builds/ndkbuild/hello-neon/app/build.gradle
            // https://developer.android.com/ndk/guides/abis
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"//, "x86" // includes ARM SO files only, so no x86 SO file
            }
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all{
            variant ->
                variant.outputs.each{
                    output->
                        output.outputFileName = "adictic_${variant.versionName}.apk"
                }
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    packagingOptions {
        exclude("META-INF/kotlinx-io.kotlin_module")
        exclude("META-INF/atomicfu.kotlin_module")
        exclude("META-INF/kotlinx-coroutines-io.kotlin_module")
        exclude("META-INF/kotlinx-coroutines-core.kotlin_module")
    }
//    flavorDimensions "abi"
//    productFlavors {
//        arm7 {
//            dimension "abi"
//            ndk.abiFilters 'armeabi-v7a'
//        }
//        x86 {
//            dimension "abi"
//            ndk.abiFilters 'x86'
//        }
//    }
}

def getCustomVersionCode() {
    def versionPropsFile = file('version.properties')
    def code
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        List<String> runTasks = gradle.startParameter.getTaskNames()
        def value = 0
        for (String item : runTasks)
            if (item.contains("assembleRelease")) {
                value = 1
            }
        if (value == 1) {
            code = Integer.parseInt(versionProps['VERSION_CODE']).intValue() + value
            versionProps['VERSION_CODE'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else
            code = Integer.parseInt(versionProps['VERSION_CODE']).intValue()
        return code
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }
}

configurations {
    download
}

dependencies {
    //AndroidX
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.work:work-runtime:2.6.0"
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    //Google
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.android.material:material:1.4.0'
    //implementation 'com.google.android.gms:play-services-ads:19.7.0'

    //Otros
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    implementation 'com.github.judemanutd:autostarter:1.1.0'

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-firestore'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version"))
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection'
    implementation 'com.squareup.okhttp3:logging-interceptor'

    //Osmdroid
    implementation "org.osmdroid:osmdroid-android:$osmdroid_version"
    implementation "org.osmdroid:osmdroid-wms:$osmdroid_version"
    implementation "org.osmdroid:osmdroid-mapsforge:$osmdroid_version"
    //implementation "org.osmdroid:osmdroid-geopackage:$osmdroid_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$versions_glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$versions_glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$versions_glide" // Glide's OkHttp3 integration.

    //Local files
    implementation(name:'joda-time-2.10.10', ext:'jar')
    implementation(name:'monthandyearpicker-1.3.0', ext:'aar')

    //Modules
    implementation project(path: ':libraries:jitsiVideocall')
    implementation project(path: ':libraries:persistentcookie')
    implementation project(path: ':libraries:common')
}

task downloadDrivers(type: Copy) {
    from configurations.download
    into "$buildDir/down-libs"
}